; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Holdit"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "Caltech DLD"
#define MyAppURL "https://github.com/caltechlibrary/holdit"
#define MyAppExeName "holdit.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{70159B80-2224-4213-B793-6156073E95E3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=Z:\project-files\library\repos\holdit\dist
OutputBaseFilename=Holdit_win10_x64
SetupIconFile=Z:\project-files\library\repos\holdit\dev\icons\generated-icons\holdit-icon-256px.ico
Compression=lzma
SolidCompression=yes
ArchitecturesInstallIn64BitMode=x64
ChangesEnvironment=true


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"


[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "\\Mac\Home\project-files\library\repos\holdit\dist\holdit.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "\\Mac\Home\project-files\library\repos\holdit\ABOUT.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "\\Mac\Home\project-files\library\repos\holdit\holdit\data\default_template.docx"; DestDir: "{app}\template.docx"; Flags: ignoreversion


[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"


; This code is from https://stackoverflow.com/a/3431379
; with the modification that I make it add the path under both HKLM and HKLM64
;
[Registry]
Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; \
    ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};{app}"; \
    Check: NeedsAddPath('{app}')
Root: HKLM64; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; \
    ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};{app}"; \
    Check: NeedsAddPath('{app}')


; This function is from https://stackoverflow.com/a/31733896/743730
;
[Code]
function NeedsAddPath(Param: string): boolean;
var
    OrigPath: string;
    ParamExpanded: string;
begin
    //expand the setup constants like {app} from Param
    ParamExpanded := ExpandConstant(Param);
    if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SYSTEM\CurrentControlSet\Control\Session Manager\Environment',
      'Path', OrigPath)
    then begin
      Result := True;
      exit;
    end;
    // look for the path with leading and trailing semicolon and with or without \ ending
    // Pos() returns 0 if not found
    Result := Pos(';' + UpperCase(ParamExpanded) + ';', ';' + UpperCase(OrigPath) + ';') = 0;  
    if Result = True then
       Result := Pos(';' + UpperCase(ParamExpanded) + '\;', ';' + UpperCase(OrigPath) + ';') = 0; 
end;
